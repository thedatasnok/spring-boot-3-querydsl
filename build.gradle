plugins {
	id 'java'
  id 'eclipse'
	id 'org.springframework.boot' version '3.0.0'
	id 'io.spring.dependency-management' version '1.1.0'
}

group = 'cool.datasnok.repros'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

sourceSets {
  main {
    java {
      srcDir "${buildDir}/generated/sources/annotationProcessor/java/main"
    }
  }
}

eclipse.classpath {
  defaultOutputDir = file("build/default")
  file.whenMerged { cp ->
    
    cp.entries.forEach { cpe ->
      if (cpe instanceof org.gradle.plugins.ide.eclipse.model.SourceFolder) {
        cpe.output = cpe.output.replace "bin/", "build/classes/java/"
      }
      if (cpe instanceof org.gradle.plugins.ide.eclipse.model.Output) {
        cpe.path = cpe.path.replace "bin/", "build/"
      }
    }
  }
}


configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'jakarta.persistence:jakarta.persistence-api'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.h2database:h2'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

  // querydsl dependencies
  implementation 'com.querydsl:querydsl-core:5.0.0'
  implementation group: 'com.querydsl', name: 'querydsl-jpa', version: '5.0.0', classifier: 'jakarta'
  annotationProcessor group: 'com.querydsl', name: 'querydsl-apt', version: '5.0.0', classifier: 'general'
}

tasks.named('test') {
	useJUnitPlatform()
}
